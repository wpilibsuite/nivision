task javaSourceJar(type: Jar) {
  description = 'Generates the source jar for java'
  group = 'WPILib'
  baseName = libraryName
  classifier = "sources"
  duplicatesStrategy = 'exclude'
  destinationDir = releaseDir
  
      dependsOn project(':arm:java').classes
      from project(':arm:java').sourceSets.main.allJava
}

task javaJavadocJar(type: Jar) {
  description = 'Generates the javadoc jar for java'
  group = 'WPILib'
  baseName = libraryName
  classifier = "javadoc"
  duplicatesStrategy = 'exclude'
  destinationDir = releaseDir
  
      dependsOn project(':arm:java').javadoc
      from project(':arm:java').javadoc.destinationDir
  }  

task userSharedZip(type: Zip) {
  description = 'Creates user zip of libraries, with shared c++ libs.'
  group = 'WPILib'
  destinationDir = releaseDir
  baseName = libraryName
  classifier = 'usershared'
  duplicatesStrategy = 'exclude'
  
  // Copy include files from cpp project
  from(file(cppInclude)) {
      include '**/*.h'
      into '/cpp/include'
  }

  // Copy shared binaries from cpp project
  project(':arm:cpp').model {
      binaries {
          withType(SharedLibraryBinarySpec) { binary ->
              from(binary.sharedLibraryFile) {
                  include '*.so'
                  into '/cpp/lib'
              }
              def debugFile = new File(binary.sharedLibraryFile.absolutePath + ".debug")
                from(debugFile) {
                    include '*.so.debug'
                    into '/cpp/lib'
                }
          }
      }
  }

  // Include java jar
  def javaProject = project(':arm:java')
  dependsOn javaProject.jar
  // Copy project java binary
  from javaProject.jar.outputs.files {
      include '*.jar'
      into '/java/lib'
  }
  
  // Copy shared binaries from driver project for java
  project(':arm:java').model {
      binaries {
          withType(SharedLibraryBinarySpec) { binary ->
              from(binary.sharedLibraryFile) {
                  include '*.so'
                  into '/java/lib'
              }
              def debugFile = new File(binary.sharedLibraryFile.absolutePath + ".debug")
                from(debugFile) {
                    include '*.so.debug'
                    into '/java/lib'
                }
          }
      }
  }
  
  // If not embedded java, include java libs
  if (!embedJavaLibraries) {
      from(file(javaLibraryLoc)) {
          include '*.jar'
          include '*.so'
          into '/java/lib'
      }
  }

  // Include java sources if set
  if (includeJavaSources) {
      dependsOn javaSourceJar
      from javaSourceJar.outputs.files {
          include '*.jar'
          into '/java/src'
      }
  }
  
  // Include java javadoc if set
  if (includeJavaJavadoc) {
      dependsOn javaJavadocJar
      from javaJavadocJar.outputs.files {
          include '*.jar'
          into '/java/src'
      }
  }
  
  // Include cpp sources if set
  if (includeCppSources) {
      from(file(cppSrc)) {
          include '**/*.cpp'
          into '/cpp/src'
      }
  }
}

task userStaticZip(type: Zip) {
  description = 'Creates user zip of libraries, with static c++ libs.'
  group = 'WPILib'
  destinationDir = releaseDir
  baseName = libraryName
  classifier = 'userstatic'
  duplicatesStrategy = 'exclude'
  
  // Copy include files from cpp project
  from(file(cppInclude)) {
      include '**/*.h'
      into '/cpp/include'
  }

  // Copy static binaries from cpp project
  project(':arm:cpp').model {
      binaries {
          withType(StaticLibraryBinarySpec) { binary ->
              from(binary.staticLibraryFile) {
                  include '*.a'
                  into '/cpp/lib'
              }
          }
      }
  }

  // copy driver include
  from (file(driverInclude)) {
      include '**/*.h'
      into '/cpp/include'
  }
  
  // Copy included driver library headers
  from(file(driverLibraryInclude)) {
      include '**/*.h'
      into '/cpp/include'
  }
  
  // Copy included driver library binaries
  from(file(driverLibraryLib)) {
      include '*.so*'
      include '*.a*'
      into '/cpp/lib'
  }
  
  // Copy included driver library headers
  from(file(cppLibraryInclude)) {
      include '**/*.h'
      into '/cpp/include'
  }
  
  // Copy included driver library binaries
  from(file(cppLibraryLib)) {
      include '*.so*'
      include '*.a*'
      into '/cpp/lib'
  }

  // Include java jar
  // Include java jar
  def javaProject = project(':arm:java')
  dependsOn javaProject.jar
  // Copy project java binary
  from javaProject.jar.outputs.files {
      include '*.jar'
      into '/java/lib'
  }
  
  // Copy shared binaries from driver project for java
  project(':arm:java').model {
      binaries {
          withType(SharedLibraryBinarySpec) { binary ->
              from(binary.sharedLibraryFile) {
                  include '*.so'
                  into '/java/lib'
              }
              def debugFile = new File(binary.sharedLibraryFile.absolutePath + ".debug")
                from(debugFile) {
                    include '*.so.debug'
                    into '/java/lib'
                }
          }
      }
  }
  
  // If not embedded java, include java libs
  if (!embedJavaLibraries) {
      from(file(javaLibraryLoc)) {
          include '*.jar'
          include '*.so'
          into '/java/lib'
      }
  }

  // Include java sources if set
  if (includeJavaSources) {
      dependsOn javaSourceJar
      from javaSourceJar.outputs.files {
          include '*.jar'
          into '/java/src'
      }
  }
  
  // Include java javadoc if set
  if (includeJavaJavadoc) {
      dependsOn javaJavadocJar
      from javaJavadocJar.outputs.files {
          include '*.jar'
          into '/java/src'
      }
  }
  
  // Include cpp sources if set
  if (includeCppSources) {
      from(file(cppSrc)) {
          include '**/*.cpp'
          into '/cpp/src'
      }
  }
  
  // Include driver sources if set
  if (includeDriverSources) {
      from(file(driverSrc)) {
          include '**/*.cpp'
          into '/cpp/src'
      }
  }
}

task cppSources(type: Zip) {
    description = 'Creates a zip of cpp sources.'
    group = 'WPILib'
    destinationDir = releaseDir
    baseName = libraryName
    classifier = 'cppsources'
    duplicatesStrategy = 'exclude'
    
    from(cppSrc) {
        into 'src'
    }

    from(cppInclude) {
        into 'include'
    }
}

project(':arm:cpp').tasks.whenTaskAdded { task ->
    def name = task.name.toLowerCase()
    if (name.contains("sharedlibrary") || name.contains("staticlibrary")) {
        userSharedZip.dependsOn task
        userStaticZip.dependsOn task
    }
}

project(':arm:java').tasks.whenTaskAdded { task ->
    def name = task.name.toLowerCase()
    if (name.contains("sharedlibrary") || name.contains("staticlibrary")) {
        userSharedZip.dependsOn task
        userStaticZip.dependsOn task
    }
}

build.dependsOn javaSourceJar
build.dependsOn javaJavadocJar
build.dependsOn userSharedZip
build.dependsOn userStaticZip
build.dependsOn cppSources
